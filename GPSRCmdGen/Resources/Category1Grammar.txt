; grammar name Category I
; grammar tier Easy

$Main     = $deliver
$Main     = $fndppl
$Main     = $fndobj

##############################################################################
#
# Manipulation
#
##############################################################################

$deliver  = $vbdeliver the {kobject} to the {placement}
$deliver  = $place the {kobject} on the {placement}
$deliver  = $vbdeliver the {kobject} to $someone
$deliver  = $vbbtake the {kobject} from the {placement 1} to the {placement 2}
$deliver  = $vbbring me the {kobject}
$deliver  = $vbbring (me | to $someoneb) the {kobject} from the {placement}
$deliver  = $takefrom and ($delivme | $delivat)
$deliver  = $takefrom and $place
$deliver  = $goplace, $vbfind the {kobject}, and ($delivme | $delivat)
$deliver  = $goplace, $vbfind the {kobject}, and $place

##############################################################################
#
# Find people
#
##############################################################################
$fndppl   = $talk to $someoneb
$fndppl   = Find $findp in the {room} and $talk
$fndppl   = $goroom, $findp, and $talk
$fndppl   = Tell me the (name | gender) of the person at the {beacon}
$fndppl   = Tell me the (name | gender) of the person in the {room}

##############################################################################
#
# Find objects
#
##############################################################################
$fndobj   = Tell me how many {kobject} there are in the {placement}
$fndobj   = Tell me how many {kobject?} there are in the {placement}
$fndobj   = $vbfind the {kobject} in the {room}
$fndobj   = $vbfind the {kobject?} in the {room}


##############################################################################
#
# Rules
#
##############################################################################

; People
$someone  = me | $someoneb
$someoneb = {name} in the {beacon 1}

; Navigating
$goplace  = $vbgopl to the {placement 1}
$gobeacon = $vbgopl to the {beacon 1}
$goroom   = $vbgopl to the {room 1}

; Take an object
$take     = $vbtake the {kobject}
$takefrom = $take from the {placement 1}

; Place an object
$place    = $vbplace it on the {placement 2}

; Deliver an object
$delivme  = $vbdeliver it to me
$delivto  = $vbdeliver it to {name}
$delivat  = $vbdeliver it to {name} in the {beacon}

; Find people
$findp    = $vbfind ( {name} | a person | someone )
$findpl   = $findp in the {beacon}

; Speak
$talk     = $answer | $speak
$answer   = answer a {question}
$speak    = $vbspeak $whattosay


##############################################################################
#
# What to say
#
##############################################################################
$whattosay = something about yourself
$whattosay = the time
$whattosay = what day is (today | tomorrow)
$whattosay = your team's (name | country | affiliation)
$whattosay = the day of the (week | month)
$whattosay = a joke

##############################################################################
#
# Verbs
#
##############################################################################
$vbbtake   = bring | take
$vbplace   = put | place
$vbbring   = bring | give
$vbdeliver = $vbbring | deliver
$vbtake   = get | grasp | take | pick up
$vbspeak  = tell | say
$vbgopl   = go | navigate
$vbgor    = $vbgopl | enter
$vbfind   = find | locate | look for
$vbguide  = guide | escort | take
$vbfollow = follow
